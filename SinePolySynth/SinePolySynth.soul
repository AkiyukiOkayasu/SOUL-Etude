graph SinePolySynth  [[main]]
{
    input event soul::midi::Message midiIn;
    output stream float audioOut;

    let
    {
        midiParser = soul::midi::MPEParser;
        voiceAllocator = soul::voice_allocators::Basic(8);
        voices = SineOsc[8];
    }

    connection
    {
        midiIn              -> midiParser.parseMIDI;
        midiParser.eventOut -> voiceAllocator.eventIn;
        voiceAllocator.voiceEventOut -> voices.eventIn;                                        
        voices.audioOut      -> audioOut;
    }
}

//==============================================================================
processor SineOsc
{
    input event (soul::note_events::NoteOn,
                 soul::note_events::NoteOff) eventIn;

    output stream float audioOut;

    event eventIn (soul::note_events::NoteOn e)
    {
        currentNote = e.note;
        phaseIncrement = float (twoPi * processor.period * soul::noteNumberToFrequency (e.note));
    }

    event eventIn (soul::note_events::NoteOff e)
    {
        if (e.note == currentNote)
            currentNote = 0;
    }

    float currentNote, phaseIncrement, amplitude;

    void run()
    {
        float phase;
        phaseIncrement = 0.0f;

        loop
        {            
            if (currentNote == 0)
                amplitude = 0.0;
            else
                amplitude = 1.0;

            phase = addModulo2Pi (phase, phaseIncrement);

            audioOut << amplitude * sin (phase);
            advance();
        }
    }
}
